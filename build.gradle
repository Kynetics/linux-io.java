plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}




def globalName    = "linux-io"

def globalVersion = "0.0.4"

def globalDesc    = "Basic Linux IO utilities for java through JNA"

def globalUrl     = "https://github.com/helins/linux-io.java"


group   = "io.helins"
version = globalVersion




repositories {
    mavenCentral()
}




dependencies {
    api "io.helins:linux-common:0.1.4"
}




java {
    withJavadocJar()
    withSourcesJar()
}




tasks.jar {
    manifest {
        attributes("Automatic-Module-Name": "io.helins.linux.io")
    }
}




javadoc {
    options.links( "https://docs.oracle.com/en/java/javase/11/docs/api/"    ,
                   "http://java-native-access.github.io/jna/4.5.1/javadoc/" ,
                   "https://javadoc.io/doc/io.helins/linux-common"          )
}




// Publishing to Maven


publishing {


    publications {
        LibMaven( MavenPublication ) {

            from components.java


            pom {


                name        = globalName
                description = globalDesc
                url         = globalUrl

                licenses {
                     license {
                         name = "Mozilla Public License Version 2.0"
                         url  = "https://www.mozilla.org/en-US/MPL/2.0/"
                     }
                }


                developers {
                    developer {
                        name            = "Adam Helinski"
                        email           = "adam@helins.io"
                        organizationUrl = "http://www.helins.io"
                    }
                }
            
            
                scm {
                    connection          = "scm:git:git://github.com/helins/linux-io.java.git"
                    developerConnection = "scm:git:ssh://github.com/helins/linux-io.java.git"
                    url                 = globalUrl
                }
            }
        }
    }


    repositories {
        maven {
            credentials( PasswordCredentials )
            name = "sonatype"
            url  = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}




signing {
    sign publishing.publications.LibMaven
}
